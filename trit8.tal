( Test 243 trit encoding in bytes
| https://compilade.net/blog/ternary-packing )

|000
 	@s1 $5 &end $1
	@s2 $5 &end $1

|100

@on-reset ( -> )
	#f300
	&>l ( -- )
		( | encode )
		STHk [ LIT2 -s1 -s1/end ]
		&>l2 ( -- )
			#01 SUB
			( :: ) STHkr #03 DIVk MUL SUB [ LIT "0 ] ADD OVR STZ
			( 3/ ) [ LITr 03 ] DIVr
			( .. ) NEQk ?&>l2
		POP2 POPr
		( | decode )
		DUP #f2 #00f3 DIV2 STH2
		[ LIT2 -s2/end -s2 ]
		&>l3 ( -- )
			( 3* ) [ LIT2r 0003 ] MUL2r
			( :: ) OVRr STHr [ LIT "0 ] ADD OVR STZ
			[ LIT2r 00ff ] AND2r
			( .. ) INC GTHk ?&>l3
		POP2 POP2r
		( | print )
		;s1 <pstr>
		#2018 DEO
		;s2 <pstr>
		#0a18 DEO
		INC GTHk ?&>l
	POP2 #010e DEO
	#800f DEO
	BRK

@<pstr> ( addr* -- )
	LDAk #18 DEO
	INC2 LDAk ?<pstr>
	POP2 JMP2r

